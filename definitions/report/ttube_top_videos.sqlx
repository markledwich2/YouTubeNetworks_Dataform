config {
  type: "incremental",
  tags:['standard'],
  dependencies:['video_stats_daily', 'channel_accepted'],
  uniqueKey:['video_id', 'period_value', 'period_type' ]
}

with relative_ranges as (
  select video_id
       , channel_id
       , dateadd(day, coalesce(f.value: offset::int, 0), (select max(date) from video_stats_daily)) as period_value
       , 'd'||f.value: range::int as period_type
       , sum(views) views
       , sum(watch_hours) watch_hours
  from video_stats_daily
     , lateral flatten(array_construct(
      object_construct('range', 1) -- this latest day is always incomplete. don't confuse people with partial results.by comparing days
    , object_construct('range', 1, 'offset', -1)
    , object_construct('range', 1, 'offset', -2)
    , object_construct('range', 1, 'offset', -3)
    , object_construct('range', 1, 'offset', -4)
    , object_construct('range', 1, 'offset', -5)
    , object_construct('range', 1, 'offset', -6)
    , object_construct('range', 1, 'offset', -7)
    , object_construct('range', 7)
    , object_construct('range', 30)
    )) f
    
  where date between dateadd(day, 1-coalesce(f.value: range::int, 0), period_value) and (select max(date) from video_stats_daily)
  group by 1, 2, 3, 4
     -- no incremental
)
   , month_ranges as (
  select video_id
       , channel_id
       , date_trunc(month, date)::date
       , 'm'
       , sum(views)
       , sum(watch_hours)
  from video_stats_daily
  where
    -- incremental: update current month
    -- full: update up to 1 year back 
    ${ when(incremental(), `date >= trunc((select max(date) from video_stats_daily), 'month')`,  
      `date>=dateadd(year, -1, (select dateadd(day, -1, max(date)) from video_stats_daily))`)}
    
  group by 1, 2, 3, 4
)
   , year_ranges as (
  select video_id
       , channel_id
       , date_trunc(year, date)::date as period_value
       , 'y'
       , sum(views) views
       , sum(watch_hours) watch_hours
  from video_stats_daily
      where
      -- incremental: update current year
      -- full: update up to 1 year back 
      ${ when(incremental(), `date >= trunc((select max(date) from video_stats_daily), 'year')`, 
      `date>=trunc(dateadd(year, -1, (select dateadd(day, -1, max(date)) from video_stats_daily)), 'year')`)}
  group by 1, 2, 3, 4
)
   , d2 as (
  select *
  from relative_ranges
  union all
  select *
  from month_ranges
  union all
  select *
  from year_ranges
)
select *
from d2