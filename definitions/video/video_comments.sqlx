config { 
    type: 'incremental',
    tags: ['standard'],
    uniqueKey: ['video_id', 'author_channel_id', 'created'],
}

with stage_latest as (
  select * from video_extra_stage
  ${ when(incremental(), `where  v:Updated > (select max(updated)::string from ${self()})`) }
  qualify row_number() over (partition by v:VideoId::string order by  v:Updated::timestamp_ntz desc) = 1
)

select
  v:VideoId::string as video_id
, v:ChannelId::string as channel_id
, h.value:Comment::string as comment
, h.value:Author::string as author
, h.value:AuthorChannelId::string as author_channel_id
, h.value:Created::timestamp_ntz as created
, v:Updated::timestamp_ntz as updated
from stage_latest
, lateral flatten(input => v:Comments::array) h
