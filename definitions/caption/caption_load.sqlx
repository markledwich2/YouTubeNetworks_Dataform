config {
  type: "incremental",
  dependencies:['video_latest'],
  tags: ['standard', 'core'],
  uniqueKey: ['video_id']
}

-- stats about captions that have been attempted load but were missing

with caps as (
  select v:Updated::timestamp_ntz updated
       , v:VideoId::string video_id
       , v:Captions::array captions
  from caption_stage c
  ${ when(incremental(), ` where v:Updated  > (SELECT MAX(updated) FROM ${self()})::string`) }
)
select c.video_id
     , max(c.updated) updated
     , any_value(vl.channel_id) channel_id
     , sum(array_size(captions))<=0 missing
from caps c
left join video_latest vl on vl.video_id=c.video_id
group by 1
