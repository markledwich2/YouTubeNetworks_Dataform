config {
  type: "incremental",
  tags:['standard', 'core'],
  dependencies:["yt_udf", "channel_latest", "channel_accepted"]
}

with latest as (
  select v:Updated::timestamp_ntz as updated
       , v:VideoId::string as video_id
       , v:Captions::array as captions
  from caption_stage s
  --and video_id = 'asQ7lsY4EJg'
  ${ when(incremental(), ` where updated > (SELECT MAX(updated) FROM ${self()})`) }
  qualify row_number() over (partition by video_id order by updated desc) = 1
)
   , flattened as (
  select *
       , t.value: Text::string as caption
       , timespantoseconds(t.value: Offset) as offset_seconds
       , row_number() over (partition by video_id order by offset_seconds) as caption_no
  from latest
     , lateral flatten(input => captions) t
)
   , cg as (
  select video_id
       , max(updated) as updated
       , listagg(caption, '\n') within group ( order by offset_seconds asc ) as caption
       , min(offset_seconds) as offset_seconds
       , floor(caption_no/20) as caption_group
  from flattened
  group by video_id
         , caption_group
)
select * from cg