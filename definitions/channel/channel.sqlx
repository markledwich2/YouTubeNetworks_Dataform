config {
  type: "incremental",
  tags: ["standard", "core"],
  dependencies: ['video_latest', 'channel_review', 'channel_sam_import' ],
  uniqueKey:['channel_id', 'updated']
}

with cstage as (
select v:ChannelId::string channel_id
     , coalesce(v:Platform::string, 'YouTube') platform
     , v:SourceId::string source_id
     , v:ChannelTitle::string channel_title
     , v:MainChannelId::string main_channel_id
     , v:LogoUrl::string logo_url
     , v:Subs::int subs
     , v:ChannelViews::int channel_views
     , v:Country::string country
     , v:Updated::timestamp_ntz updated
     , v:Status::string status_msg
     , v:"Subscriptions"::array subscriptions
     , v:Keywords::string keywords
     , v:DefaultLanguage::string default_langauge
     , v:FeaturedChannelIds::array featured_channels
     , v:Description::string description
     , v:DiscoverSource discover_source
from channel_stage
where 
    -- Filter out errors. For a day orso, YT collector picked up other platforms and created dead YouTube channels for them
    not (v:DiscoverSource is not null and v:Platform='YouTube' and v:Updated::timestamp_ntz<='2021-02-06T00:07:00')
    -- Bitchute channels not found didn't prefix their id's
    and not (v:Platform='BitChute' and v:Status='NotFound' and v:Updated::timestamp_ntz<='2021-02-07')
    -- Rumble id's wern't clean and didn't ahve the user/c channel prefix. remove thather than fix
    and not (v:Platform='Rumble' and v:Updated::timestamp_ntz<='2021-02-08' )
)
   , c1 as (
  select *, 'recfluence' as source
  from cstage as cs
  union all
  (select channel_id
      , 'YouTube'
      , null
        , channel_title
        , null as main_channel_id
        , null as logo_url
        , subs
        , null as channel_views
        , null as country
        , '2020-09-01'::timestamp_ntz as updated
        , null as status_msg
        , null as subscriptions
        , null as keywords
        , null as default_langauge
        , null as featured_channels
        , null as description
        , null as discover_source
        , 'sam_export' as source
   from channel_sam_import
   where not exists(select * from cstage where cstage.channel_id=channel_sam_import.channel_id))
)
select * from c1
${ when(incremental(), `where updated > (select max(updated) from ${self()})`)}