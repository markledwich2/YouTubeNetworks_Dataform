config {
  type: "table",
  tags: ["standard"]
}

with cvs as (
  select
    channel_id,
    sum(views) as views,
    min(upload_date) as from_date,
    max(upload_date) as to_date,
    sum(views) / nullif(datediff(day, from_date, to_date), 0) as daily_views
  from
    ${ref("video_latest")}
  group by
    channel_id
),
cs as (
  select
    c.*,
    s.views,
    s.from_date,
    s.to_date,
    s.daily_views,
    s.views * c.relevance as relevant_views,
    s.daily_views * c.relevance as relevant_daily_views
  from
    ${ref("channel_latest")} c
    left join cvs s on c.channel_id = s.channel_id
)
select
  cs.*,
  subs > 10000
  or daily_views > (
      select
        avg(daily_views / subs)
      from
        cs
      where
        subs > 0
    ) * 10000 as meets_subsviews_criteria
from
  cs
